Function GenerateWorldMap(seed)
    Randomize seed
    Notify("Initializing world")
    For x = 0 To worldSize-1
        For y = 0 To worldSize-1
            If x > 2 And x < worldSize-3 And y > 2 And y < worldSize-3
                If Rand(100) < 58
                    tempMap(x,y) = 1
                Else 
                    tempMap(x,y) = 0
                EndIf 
            Else 
                tempMap(x,y) = 0
            EndIf  
        Next y
    Next x
    
    Notify("Creating oceans and land")
    For n = 1 To 6
        For x = 0 To worldSize-1
            For y = 0 To worldSize-1
                If y = 0 Or y = worldSize-1 Or x = 0 Or x = worldSize-1
                    tempMap(x,y) = 1
                Else
                    If tempMap(x,y) = 1
                        neighbours = 0
                        For xx = Max(x-1,0) To Min(x+1,worldSize-1)
                            For yy = Max(y-1,0) To Min(y+1,worldSize-1)
                                If xx = x And yy = y
                                Else
                                    If tempMap(xx,yy) = 1 Then neighbours + 1
                                EndIf 
                            Next yy
                        Next xx
                        If neighbours >= 4 Then tempMap2(x,y) = 1 Else tempMap2(x,y) = 0
                    ElseIf tempMap(x,y) = 0
                        neighbours = 0
                        For xx = Max(x-1,0) To Min(x+1,worldSize-1)
                            For yy = Max(y-1,0) To Min(y+1,worldSize-1)
                                If xx = x And yy = y
                                Else
                                    If tempMap(xx,yy) = 1 Then neighbours + 1
                                EndIf 
                            Next yy
                        Next xx      
                        If neighbours >= 5 Then tempMap2(x,y) = 1 Else tempMap2(x,y) = 0
                    Else
                        tempMap2(x,y) = tempMap(x,y)
                    EndIf 
                EndIf 
            Next y
        Next x
    
        For x = 0 To worldSize-1
            For y = 0 To worldSize-1
                tempMap(x,y) = tempMap2(x,y)
            Next y
        Next x
    Next n
    
    Notify("Making beaches")
    For x = 0 To worldSize-1
        For y = 0 To worldSize-1
            If tempMap(x,y) = 1 
                For xx = Max(x-1,0) To Min(x+1,worldSize-1)
                    For yy = Max(y-1,0) To Min(y+1,worldSize-1)
                        If (xx = x And yy = y) Or Abs(x-xx)+Abs(y-yy)=2
                        Else
                            If tempMap(xx,yy) = 0 And Rand(100) > 40 Then tempMap(x,y) = 2
                        EndIf 
                    Next yy
                Next xx                
            EndIf 
        Next y 
    Next x
    
    Notify("Trimming beaches")
    For x = 0 To worldSize-1
        For y = 0 To worldSize-1
            If tempMap(x,y) = 2 
                beach_found = False 
                For xx = Max(x-1,0) To Min(x+1,worldSize-1)
                    For yy = Max(y-1,0) To Min(y+1,worldSize-1)
                        If (xx = x And yy = y) Or Abs(x-xx)+Abs(y-yy)=2
                        Else
                            If tempMap(xx,yy) = 2 Then beach_found = True 
                        EndIf 
                    Next yy
                Next xx     
            If beach_found = False Then tempMap(x,y) = 1
            EndIf 
        Next y 
    Next x   
    
    Notify("Growing forests")
    For x = 0 To worldSize-1
        For y = 0 To worldSize-1
            rd = Rand(-1,1)
            If tempMap(x,y) = 1
                sea_found = False 
                For r = 0 To 90
                    xr# = x
                    yr# = y
                    While Distance(x,y,xr#,yr#) < 3+rd
                        If tempMap(Int(xr#),Int(yr#)) = 0
                            sea_found = True 
                            Exit 
                        EndIf 
                        xr# = xr# + 0.5*Cos(r*4)
                        yr# = yr# + 0.5*Sin(r*4)
                    Wend
                    If sea_found = True Then Exit 
                Next r
                If sea_found = False Then tempMap(x,y) = 3
            EndIf 
        Next y 
    Next x   
    
    Notify("Making swamps")
    For x = 0 To worldSize-1
        For y = 0 To worldSize-1
            If tempMap(x,y) = 1 
                For xx = Max(x-1,0) To Min(x+1,worldSize-1)
                    For yy = Max(y-1,0) To Min(y+1,worldSize-1)
                        If (xx = x And yy = y) Or Abs(x-xx)+Abs(y-yy)=2
                        Else
                            If tempMap(xx,yy) = 0 And Rand(100) < 30 Then tempMap(x,y) = 5
                        EndIf 
                    Next yy
                Next xx                
            EndIf 
        Next y 
    Next x
    For n = 1 To 5
        For x = 0 To worldSize-1
            For y = 0 To worldSize-1
                If tempMap(x,y) = 1 Or tempMap(x,y) = 2 
                    For xx = Max(x-1,0) To Min(x+1,worldSize-1)
                        For yy = Max(y-1,0) To Min(y+1,worldSize-1)
                            If (xx = x And yy = y) Or Abs(x-xx)+Abs(y-yy)=2
                            Else
                                'If tempMap(xx,yy) = 5 And Rand(100) < 5 Then tempMap(x,y) = 5
                            EndIf 
                        Next yy
                    Next xx                
                EndIf 
            Next y 
        Next x        
    Next n
    
    Notify("Growing hills")
    For x = 0 To worldSize-1
        For y = 0 To worldSize-1
            If tempMap(x,y) = 3
                plainsFound = False 
                For r = 0 To 90
                    xr# = x
                    yr# = y
                    While Distance(x,y,xr#,yr#) < 3
                        If tempMap(Int(xr#),Int(yr#)) = 1
                            plainsFound = True 
                            Exit 
                        EndIf 
                        xr# = xr# + 0.5*Cos(r*4)
                        yr# = yr# + 0.5*Sin(r*4)
                    Wend
                    If plainsFound = True Then Exit 
                Next r
                If plainsFound = False Then tempMap(x,y) = 6
            EndIf 
        Next y 
    Next x   
    
    Notify("Smoothing hillside")
    For x = 0 To worldSize-1
        For y = 0 To worldSize-1
            If tempMap(x,y) = 3 
                For xx = Max(x-1,0) To Min(x+1,worldSize-1)
                    For yy = Max(y-1,0) To Min(y+1,worldSize-1)
                        If (xx = x And yy = y) Or Abs(x-xx)+Abs(y-yy)=2
                        Else
                            If tempMap(xx,yy) = 6 Then tempMap(x,y) = 7
                        EndIf 
                    Next yy
                Next xx                
            EndIf 
        Next y 
    Next x
    
    Notify("Building towns")
    town_count = 0
    While town_count <= 10
        attempts = attempts + 1
        x = Rand(worldSize-1)
        y = Rand(worldSize-1)
        If tempMap(x,y) = 1
			tempMap(x,y) = 4
			town_count = town_count + 1
        EndIf 
    Wend 
    
    Notify("Finishing")
    For wrsq.worldSquare = Each worldSquare
        Delete wrsq
    Next wrsq
    
    id = 0
    For y = 0 To worldSize-1
        For x = 0 To worldSize-1
            wrsq.worldSquare = New(worldSquare)
            wrsq\x = x
            wrsq\y = y
            wrsq\id = id
			wrsq\visited = false
            id = id+1
            If x = 0 or y = 0 Or x = worldSize-1 or y = worldSize-1
                'wrsq\biome = 0
            Else 
                wrsq\biome = tempMap(x,y)
            EndIf 
            If wrsq\biome = 4 Then wrsq\townName = RandomName()
            
        Next x
    Next y
    ClearText 
End Function 

Function RegionCount()
	c = 0
	For wrsq.worldSquare = Each worldSquare
		if wrsq\visited = true then c+1
	Next wrsq
	return c
End Function 

Function GenerateRegionFloor(id)
	img = MakeImage(regionSize*BOX_SIZE,regionSize*BOX_SIZE)
	DrawToImage img
	For x = 0 To regionSize-1
		For y = 0 To regionSize-1
			GetBiomeColor(id)    
			Box x*BOX_SIZE,y*BOX_SIZE, BOX_SIZE, BOX_SIZE
		Next y
	Next x
    DrawToScreen 
	return img
End Function 

Function Travel(x,y)
	'Loading()
    For wrsq.worldSquare = Each worldSquare
        If wrsq\id = playerRegion
            current_x = wrsq\x
            current_y = wrsq\y
            Exit 
        EndIf 
    Next wrsq
    For wrsq.worldSquare = Each worldSquare
        If wrsq\x = current_x+x And wrsq\y = current_y+y 
			wrsq\visited = true
            playerRegion = wrsq\id
        EndIf 
    Next wrsq    
    UpdateLOS()
End Function 

Function GetBiome(id)
    If id = 0
        Return "ocean"
    ElseIf id = 1
        Return "plains"
    ElseIf id = 2
        Return "beach"
    ElseIf id = 3
        Return "forest"
    ElseIf id = 4
        Return "town"
    ElseIf id = 5
        Return "swamp"
    ElseIf id = 6
        Return "mountain"
    ElseIf id = 7
        Return "hillside"
    EndIf 
End Function 

Function LoadBiomeImgs(n)
	for i = 0 to n
		biomeImg(i,0) = Loadimage("gfx/"+i+".png")
		if i = 3
			for x = 1 to 3
				biomeImg(i,x) = Loadimage("gfx/"+i+"_"+x+".png")
			next x
		elseif i = 6
			for x = 1 to 2
				biomeImg(i,x) = Loadimage("gfx/"+i+"_"+x+".png")
			next x			
		endif 
	next i
End Function 

Function GetBiomeColor(id)
    If id = 0
        Color 0,0,240+Rand(-15,15)
    ElseIf id = 1
        Color 0,240+Rand(-15,15),0
    ElseIf id = 2
        Color 200+Rand(-15,15),255+Rand(-30,0),0
    ElseIf id = 3
        Color 0,Rand(125,170),0
    ElseIf id = 4
        Color 255,0,0
    ElseIf id = 5
        Color 95,125+Rand(-15,15),15
    ElseIf id = 6
        Color Rand(230,255),Rand(230,255),Rand(230,255)
    ElseIf id = 7
        Color Rand(75,125),Rand(125,170),Rand(75,125)
    EndIf     
End Function 

Function UpdateLOS()
	for x = 0 to worldSize
		for y = 0 to worldSize
			if sight(x,y) = -2 then sight(x,y) = -1
		next y
	next x
    For wrsq.worldSquare = Each worldSquare
        If wrsq\id = playerRegion
            x = wrsq\x
            y = wrsq\y
			sight(x,y) = -2
        EndIf
    Next wrsq
    For r = 0 To 90
		xx# = x
		yy# = y
		steps = 10
		n = 0
        while steps > 0
			xx# = min(worldSize-1,max(0,xx# + cos(r*4)))
			yy# = min(worldSize-1,max(0,yy# + sin(r*4)))
			if biome(int(xx#),int(yy#)) = 0
				steps = steps-1
			elseif biome(int(xx#),int(yy#)) = 1 or biome(int(xx#),int(yy#)) = 2 or biome(int(xx#),int(yy#)) = 4 or biome(int(xx#),int(yy#)) = 5
				steps = steps-3
			elseif biome(int(xx#),int(yy#)) = 7 or biome(int(xx#),int(yy#)) = 6 
				steps = steps-6
			else 
				steps = steps-4
			endif 
			sight(int(xx#),int(yy#)) = -2
			
			n = n+1
			if n > 100 then exit
		wend
    Next r
End Function 

function IdToBiome(id)
	for wrsq.worldSquare = each worldSquare
		if wrsq\id = id
			return wrsq\biome
		endif
	next wrsq
end function 

function SquareOnScreen(x,y)
	if (x+worldOffsetX < regionSize) and (y+worldOffsetY < regionSize) then return true else return false
end function 

function FixOffset()
	for wrsq.worldSquare = each worldSquare
		if wrsq\id = playerRegion
			player_x = wrsq\x
			player_y = wrsq\y
		endif
	next wrsq
	
	if player_x+5 >= regionSize-worldOffsetX then worldOffsetX = max(regionSize-worldSize,worldOffsetX-1)
	if player_y+5 >= regionSize-worldOffsetY then worldOffsetY = max(regionSize-worldSize,worldOffsetY-1)
	if player_x-5 < -worldOffsetX then worldOffsetX = min(0,worldOffsetX+1)
	if player_y-5 < -worldOffsetY then worldOffsetY = min(0,worldOffsetY+1)
end function 

function FogThickness(x,y,player_x,player_y)
	d = 3*distance( x,y,player_x,player_y)
	return max(1,min(70,d))
end function 