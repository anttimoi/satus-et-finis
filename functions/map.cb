Function GenerateWorldMap(seed)
    Randomize seed
    Notify("Initializing world")
    For x = 0 To WORLD_SIZE-1
        For y = 0 To WORLD_SIZE-1
            If x > 2 And x < WORLD_SIZE-3 And y > 2 And y < WORLD_SIZE-3
                If Rand(100) < 58
                    temp_map(x,y) = 1
                Else 
                    temp_map(x,y) = 0
                EndIf 
            Else 
                temp_map(x,y) = 0
            EndIf  
        Next y
    Next x
    
    Notify("Creating oceans and land")
    For n = 1 To 6
        For x = 0 To WORLD_SIZE-1
            For y = 0 To WORLD_SIZE-1
                If y = 0 Or y = WORLD_SIZE-1 Or x = 0 Or x = WORLD_SIZE-1
                    temp_map(x,y) = 1
                Else
                    If temp_map(x,y) = 1
                        neighbours = 0
                        For xx = Max(x-1,0) To Min(x+1,WORLD_SIZE-1)
                            For yy = Max(y-1,0) To Min(y+1,WORLD_SIZE-1)
                                If xx = x And yy = y
                                Else
                                    If temp_map(xx,yy) = 1 Then neighbours + 1
                                EndIf 
                            Next yy
                        Next xx
                        If neighbours >= 4 Then temp_map2(x,y) = 1 Else temp_map2(x,y) = 0
                    ElseIf temp_map(x,y) = 0
                        neighbours = 0
                        For xx = Max(x-1,0) To Min(x+1,WORLD_SIZE-1)
                            For yy = Max(y-1,0) To Min(y+1,WORLD_SIZE-1)
                                If xx = x And yy = y
                                Else
                                    If temp_map(xx,yy) = 1 Then neighbours + 1
                                EndIf 
                            Next yy
                        Next xx      
                        If neighbours >= 5 Then temp_map2(x,y) = 1 Else temp_map2(x,y) = 0
                    Else
                        temp_map2(x,y) = temp_map(x,y)
                    EndIf 
                EndIf 
            Next y
        Next x
    
        For x = 0 To WORLD_SIZE-1
            For y = 0 To WORLD_SIZE-1
                temp_map(x,y) = temp_map2(x,y)
            Next y
        Next x
    Next n
    
    Notify("Making beaches")
    For x = 0 To WORLD_SIZE-1
        For y = 0 To WORLD_SIZE-1
            If temp_map(x,y) = 1 
                For xx = Max(x-1,0) To Min(x+1,WORLD_SIZE-1)
                    For yy = Max(y-1,0) To Min(y+1,WORLD_SIZE-1)
                        If (xx = x And yy = y) Or Abs(x-xx)+Abs(y-yy)=2
                        Else
                            If temp_map(xx,yy) = 0 And Rand(100) > 40 Then temp_map(x,y) = 2
                        EndIf 
                    Next yy
                Next xx                
            EndIf 
        Next y 
    Next x
    
    Notify("Trimming beaches")
    For x = 0 To WORLD_SIZE-1
        For y = 0 To WORLD_SIZE-1
            If temp_map(x,y) = 2 
                beach_found = False 
                For xx = Max(x-1,0) To Min(x+1,WORLD_SIZE-1)
                    For yy = Max(y-1,0) To Min(y+1,WORLD_SIZE-1)
                        If (xx = x And yy = y) Or Abs(x-xx)+Abs(y-yy)=2
                        Else
                            If temp_map(xx,yy) = 2 Then beach_found = True 
                        EndIf 
                    Next yy
                Next xx     
            If beach_found = False Then temp_map(x,y) = 1
            EndIf 
        Next y 
    Next x   
    
    Notify("Growing forests")
    For x = 0 To WORLD_SIZE-1
        For y = 0 To WORLD_SIZE-1
            rd = Rand(-1,1)
            If temp_map(x,y) = 1
                sea_found = False 
                For r = 0 To 90
                    xr# = x
                    yr# = y
                    While Distance(x,y,xr#,yr#) < 3+rd
                        If temp_map(Int(xr#),Int(yr#)) = 0
                            sea_found = True 
                            Exit 
                        EndIf 
                        xr# = xr# + 0.5*Cos(r*4)
                        yr# = yr# + 0.5*Sin(r*4)
                    Wend
                    If sea_found = True Then Exit 
                Next r
                If sea_found = False Then temp_map(x,y) = 3
            EndIf 
        Next y 
    Next x   
    
    Notify("Making swamps")
    For x = 0 To WORLD_SIZE-1
        For y = 0 To WORLD_SIZE-1
            If temp_map(x,y) = 1 
                For xx = Max(x-1,0) To Min(x+1,WORLD_SIZE-1)
                    For yy = Max(y-1,0) To Min(y+1,WORLD_SIZE-1)
                        If (xx = x And yy = y) Or Abs(x-xx)+Abs(y-yy)=2
                        Else
                            If temp_map(xx,yy) = 0 And Rand(100) < 30 Then temp_map(x,y) = 5
                        EndIf 
                    Next yy
                Next xx                
            EndIf 
        Next y 
    Next x
    For n = 1 To 5
        For x = 0 To WORLD_SIZE-1
            For y = 0 To WORLD_SIZE-1
                If temp_map(x,y) = 1 Or temp_map(x,y) = 2 
                    For xx = Max(x-1,0) To Min(x+1,WORLD_SIZE-1)
                        For yy = Max(y-1,0) To Min(y+1,WORLD_SIZE-1)
                            If (xx = x And yy = y) Or Abs(x-xx)+Abs(y-yy)=2
                            Else
                                'If temp_map(xx,yy) = 5 And Rand(100) < 5 Then temp_map(x,y) = 5
                            EndIf 
                        Next yy
                    Next xx                
                EndIf 
            Next y 
        Next x        
    Next n
    
    Notify("Growing hills")
    For x = 0 To WORLD_SIZE-1
        For y = 0 To WORLD_SIZE-1
            If temp_map(x,y) = 3
                plains_found = False 
                For r = 0 To 90
                    xr# = x
                    yr# = y
                    While Distance(x,y,xr#,yr#) < 3
                        If temp_map(Int(xr#),Int(yr#)) = 1
                            plains_found = True 
                            Exit 
                        EndIf 
                        xr# = xr# + 0.5*Cos(r*4)
                        yr# = yr# + 0.5*Sin(r*4)
                    Wend
                    If plains_found = True Then Exit 
                Next r
                If plains_found = False Then temp_map(x,y) = 6
            EndIf 
        Next y 
    Next x   
    
    Notify("Smoothing hillside")
    For x = 0 To WORLD_SIZE-1
        For y = 0 To WORLD_SIZE-1
            If temp_map(x,y) = 3 
                For xx = Max(x-1,0) To Min(x+1,WORLD_SIZE-1)
                    For yy = Max(y-1,0) To Min(y+1,WORLD_SIZE-1)
                        If (xx = x And yy = y) Or Abs(x-xx)+Abs(y-yy)=2
                        Else
                            If temp_map(xx,yy) = 6 Then temp_map(x,y) = 7
                        EndIf 
                    Next yy
                Next xx                
            EndIf 
        Next y 
    Next x
    
    Notify("Building towns")
    town_count = 0
    While town_count <= 10
        attempts = attempts + 1
        x = Rand(WORLD_SIZE-1)
        y = Rand(WORLD_SIZE-1)
        If temp_map(x,y) = 1
			temp_map(x,y) = 4
			town_count = town_count + 1
        EndIf 
    Wend 
    
    Notify("Finishing")
    For wrsq.WORLD_SQUARE = Each WORLD_SQUARE
        Delete wrsq
    Next wrsq
    
    id = 0
    For y = 0 To WORLD_SIZE-1
        For x = 0 To WORLD_SIZE-1
            wrsq.WORLD_SQUARE = New(WORLD_SQUARE)
            wrsq\x = x
            wrsq\y = y
            wrsq\id = id
            id = id+1
            If x = 0 or y = 0 Or x = WORLD_SIZE-1 or y = WORLD_SIZE-1
                'wrsq\biome = 0
            Else 
                wrsq\biome = temp_map(x,y)
            EndIf 
            If wrsq\biome = 4 Then wrsq\town_name = RandomName()
            
        Next x
    Next y
    ClearText 
End Function 

Function RegionCount()
    count = 0
    For i = 0 To WORLD_SIZE^2
        If region(i) > 0 Then count = count + 1
    Next i
    Return count 
End Function 

Function GenerateRegionFloor(id)
	img = MakeImage(REGION_SIZE*BOX_SIZE,REGION_SIZE*BOX_SIZE)
	DrawToImage img
	For x = 0 To REGION_SIZE-1
		For y = 0 To REGION_SIZE-1
			GetBiomeColor(id)    
			Box x*BOX_SIZE,y*BOX_SIZE, BOX_SIZE, BOX_SIZE
		Next y
	Next x
    DrawToScreen 
	return img
End Function 

Function Travel(x,y)
	'Loading()
    For wrsq.WORLD_SQUARE = Each WORLD_SQUARE
        If wrsq\id = PLAYER_REGION
            current_x = wrsq\x
            current_y = wrsq\y
            Exit 
        EndIf 
    Next wrsq
    For wrsq.WORLD_SQUARE = Each WORLD_SQUARE
        If wrsq\x = current_x+x And wrsq\y = current_y+y 
            If region(wrsq\id) = 0
                region(wrsq\id) = 1
            EndIf 
            PLAYER_REGION = wrsq\id
            if x <> 0 then PLAYER_X = Max(0,(REGION_SIZE-1)*BOX_SIZE*x*-1)
            if y <> 0 then PLAYER_Y = Max(0,(REGION_SIZE-1)*BOX_SIZE*y*-1)
        EndIf 
    Next wrsq    
    DRAW_REGION_BIOME = IdToBiome(PLAYER_REGION)
	REGION_FLOOR_IMG = GenerateRegionFloor(DRAW_REGION_BIOME)
    UpdateLOS()
End Function 

Function GetBiome(id)
    If id = 0
        Return "ocean"
    ElseIf id = 1
        Return "plains"
    ElseIf id = 2
        Return "beach"
    ElseIf id = 3
        Return "forest"
    ElseIf id = 4
        Return "town"
    ElseIf id = 5
        Return "swamp"
    ElseIf id = 6
        Return "mountain"
    ElseIf id = 7
        Return "hillside"
    EndIf 
End Function 

Function LoadBiomeImgs(n)
	for i = 0 to n
		Biome_Img(i,0) = Loadimage("gfx/"+i+".png")
		if i = 3 then Biome_Img(i,1) = Loadimage("gfx/"+i+"_1.png")
	next i
End Function 

Function GetBiomeColor(id)
    If id = 0
        Color 0,0,240+Rand(-15,15)
    ElseIf id = 1
        Color 0,240+Rand(-15,15),0
    ElseIf id = 2
        Color 200+Rand(-15,15),255+Rand(-30,0),0
    ElseIf id = 3
        Color 0,Rand(125,170),0
    ElseIf id = 4
        Color 255,0,0
    ElseIf id = 5
        Color 95,125+Rand(-15,15),15
    ElseIf id = 6
        Color Rand(230,255),Rand(230,255),Rand(230,255)
    ElseIf id = 7
        Color Rand(75,125),Rand(125,170),Rand(75,125)
    EndIf     
End Function 

Function UpdateLOS()
    For wrsq.WORLD_SQUARE = Each WORLD_SQUARE
        If wrsq\id = PLAYER_REGION
            x = wrsq\x
            y = wrsq\y
        EndIf
    Next wrsq
    For r = 0 To 90
		xx# = x
		yy# = y
		steps = 10
		n = 0
        while steps > 0
			xx# = min(WORLD_SIZE-1,max(0,xx# + cos(r*4)))
			yy# = min(WORLD_SIZE-1,max(0,yy# + sin(r*4)))
			if sight(int(xx#),int(yy#)) = 0
				steps = steps-1
			elseif sight(int(xx#),int(yy#)) = 1 or sight(int(xx#),int(yy#)) = 2 or sight(int(xx#),int(yy#)) = 4 or sight(int(xx#),int(yy#)) = 5
				steps = steps-2
			elseif sight(int(xx#),int(yy#)) = 7 or sight(int(xx#),int(yy#)) = 6 
				steps = steps-5
			else 
				steps = steps-4
			endif 
			sight(int(xx#),int(yy#)) = -1
			n = n+1
			if n > 100 then exit
		wend
    Next r
End Function 

function IdToBiome(id)
	for wrsq.WORLD_SQUARE = each WORLD_SQUARE
		if wrsq\id = id
			return wrsq\biome
		endif
	next wrsq
end function 