SCREEN 1280,720
FrameLimit 60 
Global WORLD_SIZE, REGION_SIZE, WORLD_SEED, BOX_SIZE, REGION_ARRAY_SIZE
ClearArray OFF 
WORLD_SIZE = 70
REGION_SIZE = 70
WORLD_SEED = 123441234+3
BOX_SIZE = 10
REGION_ARRAY_SIZE = 0

Dim temp_map(WORLD_SIZE-1,WORLD_SIZE-1)
Dim temp_map2(WORLD_SIZE-1,WORLD_SIZE-1)
Global temp_map()
Global temp_map2()

Dim region(WORLD_SIZE^2)
Global region()
For i = 0 To WORLD_SIZE^2
    region(i) = -1
Next i

Type WORLD_SQUARE
    Field id
    Field x
    Field y
    Field biome
    Field town_name As String
End Type 

Global PLAYER_X, PLAYER_Y, PLAYER_REGION, PLAYER_SPEED
PLAYER_X = 350
PLAYER_Y = 350
PLAYER_SPEED = 5
PLAYER_REGION = -1

REGION_FLOOR = MakeImage(REGION_SIZE*BOX_SIZE,REGION_SIZE*BOX_SIZE)
Global REGION_FLOOR

GenerateWorldMap(WORLD_SEED,58)

Global DRAW_REGION
DRAW_REGION = -1
Repeat

    Color cbwhite
    SetWindow Str(FPS()+" SEED:"+WORLD_SEED)
    Text 720,30, "Regions generated: "+RegionCount()
    
    If DRAW_REGION = -1
        Randomize WORLD_SEED
        For wrsq.WORLD_SQUARE = Each WORLD_SQUARE
            GetBiomeColor(wrsq\biome)  
            If wrsq\id = PLAYER_REGION And (Int(Timer()/500) Mod 2) = 0  Then Color cbmagenta
            Box wrsq\x*BOX_SIZE+10,wrsq\y*BOX_SIZE+10, BOX_SIZE, BOX_SIZE
            If (MouseX()-10)/BOX_SIZE = wrsq\x And (MouseY()-10)/BOX_SIZE = wrsq\y
                Text 720,10,wrsq\x+" "+wrsq\y+" "+wrsq\id+" "+GetBiome(wrsq\biome)+" "+wrsq\town_name
                If MouseHit(1)
                    If region(wrsq\id) = -1
                        region(wrsq\id) = wrsq\biome
                    EndIf
                    DRAW_REGION = wrsq\id
                    If PLAYER_REGION <> -1 Then DRAW_REGION = PLAYER_REGION Else PLAYER_REGION = DRAW_REGION
                    REGION_FLOOR = GenerateRegionFloor()
                EndIf 
            EndIf 
        Next wrsq
        Randomize Timer()+Rand(1000)
    Else
        DrawImage REGION_FLOOR, 10, 10
        If DRAW_REGION = PLAYER_REGION
            Color cbmagenta
            Circle PLAYER_X+10, PLAYER_Y+10, 10, 1
            PLAYER_X = Min((REGION_SIZE-1)*BOX_SIZE,Max(0,PLAYER_X + PLAYER_SPEED*(RightKey()-LeftKey())))
            PLAYER_Y = Min((REGION_SIZE-1)*BOX_SIZE,Max(0,PLAYER_Y + PLAYER_SPEED*(DownKey()-UpKey())))
            
            If PLAYER_X = (REGION_SIZE-1)*BOX_SIZE
                Text 720,70, "Travel East?"
                If KeyHit(cbkeyreturn) Then Travel(1,0)
            ElseIf PLAYER_X = 0
                Text 720,70, "Travel West?"
                If KeyHit(cbkeyreturn) Then Travel(-1,0)
            ElseIf PLAYER_Y = (REGION_SIZE-1)*BOX_SIZE
                Text 720,70, "Travel South?"
                If KeyHit(cbkeyreturn) Then Travel(0,1)
            ElseIf PLAYER_Y = 0
                Text 720,70, "Travel North?"
                If KeyHit(cbkeyreturn) Then Travel(0,-1)
            EndIf 
        EndIf 
        
        Text 720,10, DRAW_REGION+" "+GetBiome(region(DRAW_REGION))
        Text 720,50, PLAYER_X+" "+PLAYER_Y
        
        If MouseHit(2) Then DRAW_REGION = -1
    EndIf 
    
    DrawScreen 
    
    If KeyHit(cbkeyescape) Then Exit 
    
Forever
Function RegionCount()
    count = 0
    For i = 0 To WORLD_SIZE^2
        If region(i) <> -1 Then count = count + 1
    Next i
    Return count 
End Function 

Function GenerateRegionFloor()
    img = MakeImage(REGION_SIZE*BOX_SIZE,REGION_SIZE*BOX_SIZE)
    DrawToImage img
    For x = 0 To REGION_SIZE-1
        For y = 0 To REGION_SIZE-1
            GetFloorColor(region(DRAW_REGION))    
            Box x*BOX_SIZE,y*BOX_SIZE, BOX_SIZE, BOX_SIZE
        Next y
    Next x
    DrawToScreen 
    Return img
End Function 

Function Travel(x,y)
    For wrsq.WORLD_SQUARE = Each WORLD_SQUARE
        If wrsq\id = DRAW_REGION
            current_x = wrsq\x
            current_y = wrsq\y
            Exit 
        EndIf 
    Next wrsq
    For wrsq.WORLD_SQUARE = Each WORLD_SQUARE
        If wrsq\x = current_x+x And wrsq\y = current_y+y 
            If region(wrsq\id) = -1
                region(wrsq\id) = wrsq\biome
            EndIf 
            DRAW_REGION = wrsq\id
            PLAYER_REGION = DRAW_REGION
            if x <> 0 then PLAYER_X = Max(0,(REGION_SIZE-1)*BOX_SIZE*x*-1)
            if y <> 0 then PLAYER_Y = Max(0,(REGION_SIZE-1)*BOX_SIZE*y*-1)
        EndIf 
    Next wrsq    
    REGION_FLOOR = GenerateRegionFloor()
End Function 

Function GetBiome(id)
    If id = 0
        Return "ocean"
    ElseIf id = 1
        Return "plains"
    ElseIf id = 2
        Return "beach"
    ElseIf id = 3
        Return "forest"
    ElseIf id = 4
        Return "town"
    EndIf 
End Function 

Function GetBiomeColor(id)
    If id = 0
        Color 0,0,240+Rand(-15,15)
    ElseIf id = 1
        Color 0,240+Rand(-15,15),0
    ElseIf id = 2
        Color 200+Rand(-15,15),255+Rand(-30,0),0
    ElseIf id = 3
        Color 0,Rand(125,170),0
    ElseIf id = 4
        Color 255,0,0
    EndIf     
End Function 

Function GetFloorColor(id)
    If id = 0
        Color 0,0,240+Rand(-15,15)
    ElseIf id = 1   
        Color 0,240+Rand(-15,15),0
    ElseIf id = 2
        Color 200+Rand(-15,15),255+Rand(-30,0),0
    EndIf 
End Function 

Function GenerateWorldMap(seed,ratio)
    Randomize seed
    Notify("Initializing world")
    For x = 0 To WORLD_SIZE-1
        For y = 0 To WORLD_SIZE-1
            If x > 2 And x < WORLD_SIZE-3 And y > 2 And y < WORLD_SIZE-3
                If Rand(100) < ratio
                    temp_map(x,y) = 1
                Else 
                    temp_map(x,y) = 0
                EndIf 
            Else 
                temp_map(x,y) = 0
            EndIf  
        Next y
    Next x
    
    Notify("Creating oceans and land")
    For n = 1 To 6
        For x = 0 To WORLD_SIZE-1
            For y = 0 To WORLD_SIZE-1
                If y = 0 Or y = WORLD_SIZE-1 Or x = 0 Or x = WORLD_SIZE-1
                    temp_map(x,y) = 1
                Else
                    If temp_map(x,y) = 1
                        neighbours = 0
                        For xx = Max(x-1,0) To Min(x+1,WORLD_SIZE-1)
                            For yy = Max(y-1,0) To Min(y+1,WORLD_SIZE-1)
                                If xx = x And yy = y
                                Else
                                    If temp_map(xx,yy) = 1 Then neighbours + 1
                                EndIf 
                            Next yy
                        Next xx
                        If neighbours >= 4 Then temp_map2(x,y) = 1 Else temp_map2(x,y) = 0
                    ElseIf temp_map(x,y) = 0
                        neighbours = 0
                        For xx = Max(x-1,0) To Min(x+1,WORLD_SIZE-1)
                            For yy = Max(y-1,0) To Min(y+1,WORLD_SIZE-1)
                                If xx = x And yy = y
                                Else
                                    If temp_map(xx,yy) = 1 Then neighbours + 1
                                EndIf 
                            Next yy
                        Next xx      
                        If neighbours >= 5 Then temp_map2(x,y) = 1 Else temp_map2(x,y) = 0
                    Else
                        temp_map2(x,y) = temp_map(x,y)
                    EndIf 
                EndIf 
            Next y
        Next x
    
        For x = 0 To WORLD_SIZE-1
            For y = 0 To WORLD_SIZE-1
                temp_map(x,y) = temp_map2(x,y)
            Next y
        Next x
    Next n
    
    Notify("Making beaches")
    For x = 0 To WORLD_SIZE-1
        For y = 0 To WORLD_SIZE-1
            If temp_map(x,y) = 1 
                For xx = Max(x-1,0) To Min(x+1,WORLD_SIZE-1)
                    For yy = Max(y-1,0) To Min(y+1,WORLD_SIZE-1)
                        If xx = x And yy = y
                        Else
                            If temp_map(xx,yy) = 0 And Rand(100) > 40 Then temp_map(x,y) = 2
                        EndIf 
                    Next yy
                Next xx                
            EndIf 
        Next y 
    Next x
    
    Notify("Trimming beaches")
    For x = 0 To WORLD_SIZE-1
        For y = 0 To WORLD_SIZE-1
            If temp_map(x,y) = 2 
                beach_found = False 
                For xx = Max(x-1,0) To Min(x+1,WORLD_SIZE-1)
                    For yy = Max(y-1,0) To Min(y+1,WORLD_SIZE-1)
                        If xx = x And yy = y
                        Else
                            If temp_map(xx,yy) = 2 Then beach_found = True 
                        EndIf 
                    Next yy
                Next xx     
            If beach_found = False Then temp_map(x,y) = 1
            EndIf 
        Next y 
    Next x   
    
    Notify("Growing forests")
    For x = 0 To WORLD_SIZE-1
        For y = 0 To WORLD_SIZE-1
            rd = Rand(-1,1)
            If temp_map(x,y) = 1
                sea_found = False 
                For r = 0 To 90
                    xr# = x
                    yr# = y
                    While Distance(x,y,xr#,yr#) < 4+rd
                        If temp_map(Int(xr#),Int(yr#)) = 0
                            sea_found = True 
                            Exit 
                        EndIf 
                        xr# = xr# + 0.5*Cos(r*4)
                        yr# = yr# + 0.5*Sin(r*4)
                    Wend
                    If sea_found = True Then Exit 
                Next r
                If sea_found = False Then temp_map(x,y) = 3
            EndIf 
        Next y 
    Next x   
    
    Notify("Building towns")
    town_count = 0
    While town_count <= 10
        x = Rand(WORLD_SIZE-1)
        y = Rand(WORLD_SIZE-1)
        If temp_map(x,y) = 1
            town_found = True
            For xx = Max(x-1,0) To Min(x+1,WORLD_SIZE-1)
                For yy = Max(y-1,0) To Min(y+1,WORLD_SIZE-1)
                    If xx = x And yy = y
                    Else
                        If temp_map(xx,yy) <> 1 Then town_found = False
                    EndIf 
                Next yy
            Next xx     
            If town_found = True
                temp_map(x,y) = 4
                town_count = town_count + 1
            EndIf 
        EndIf 
    Wend 
    
    Notify("Finishing")
    For wrsq.WORLD_SQUARE = Each WORLD_SQUARE
        Delete wrsq
    Next wrsq
    
    id = 0
    For y = 0 To WORLD_SIZE-1
        For x = 0 To WORLD_SIZE-1
            wrsq.WORLD_SQUARE = New(WORLD_SQUARE)
            wrsq\x = x
            wrsq\y = y
            wrsq\id = id
            id = id+1
            If x = 0 or y = 0 Or x = WORLD_SIZE-1 or y = WORLD_SIZE-1
                'wrsq\biome = 0
            Else 
                wrsq\biome = temp_map(x,y)
            EndIf 
            If wrsq\biome = 4 Then wrsq\town_name = RandomName()
            
        Next x
    Next y
    ClearText 
End Function 

Function Notify(notification$)
    AddText notification$
    DrawScreen 
End Function 


Function RandomName()
    
    voc_max = 5
    cons_max = 15
    Dim vocals(voc_max) As String  // a e i o u y
    Dim consonants(cons_max) As String // m n p b t d k g f v s z x h r l
    
    vocals(0) = "a"
    vocals(1) = "e"
    vocals(2) = "i"
    vocals(3) = "o"
    vocals(4) = "u"
    vocals(5) = "y"
    
    consonants(0) = "m" 
    consonants(1) = "n" 
    consonants(2) = "p" 
    consonants(3) = "b" 
    consonants(4) = "t" 
    consonants(5) = "d" 
    consonants(6) = "k" 
    consonants(7) = "g" 
    consonants(8) = "f" 
    consonants(9) = "v" 
    consonants(10) = "s" 
    consonants(11) = "z" 
    consonants(12) = "x" 
    consonants(13) = "h" 
    consonants(14) = "r" 
    consonants(15) = "l" 
    
    name$ = ""
    c = -1
    r = Rand(2)
    If r = 0
        name$ = Upper(consonants(Rand(15)))+vocals(Rand(5))+consonants(Rand(15))
    ElseIf r = 1
        name$ = Upper(vocals(Rand(5)))+consonants(Rand(15))+vocals(Rand(5))
    ElseIf r = 2
        name$ = Upper(vocals(Rand(5)))+vocals(Rand(5))
    EndIf 
    r = Rand(3)    
    If r = 0
        name$ = name$ + vocals(Rand(5))+vocals(Rand(5))
    ElseIf r = 1
        rr = Rand(15)
        name$ = name$ + consonants(Rand(15))+vocals(Rand(5))
    ElseIf r = 2
        name$ = name$ + vocals(Rand(5))
    ElseIf r = 3
        c = Rand(15)
        name$ = name$ + consonants(c)
    EndIf
    r = Rand(2)
    
    If r = 0
        name$ = name$ + consonants(Rand(15))+vocals(Rand(5))+consonants(Rand(15))
    ElseIf r = 1
        name$ = name$ + vocals(Rand(5))
    ElseIf r = 2
        If c > -1
            name$ = name$ + consonants(c)
        Else 
            name$ = name$ + consonants(Rand(15))
        EndIf 
    EndIf 
    
    If Rand(5) = 1
        name$ = name$ + "-"
        r = Rand(2)
        If r = 0
            name$ = name$ + Upper(consonants(Rand(15)))+vocals(Rand(5))+consonants(Rand(15))
        ElseIf r = 1
            name$ = name$ + Upper(vocals(Rand(5)))+consonants(Rand(15))+vocals(Rand(5))
        ElseIf r = 2
            name$ = name$ + Upper(vocals(Rand(5)))+vocals(Rand(5))
        EndIf 
        r = Rand(3)    
        If r = 0
            name$ = name$ + vocals(Rand(5))+vocals(Rand(5))
        ElseIf r = 1
            rr = Rand(15)
            name$ = name$ + consonants(Rand(15))+vocals(Rand(5))
        ElseIf r = 2
            name$ = name$ + vocals(Rand(5))
        ElseIf r = 3
            c = Rand(15)
            name$ = name$ + consonants(c)
        EndIf
        r = Rand(2)
        
        If r = 0
            name$ = name$ + consonants(Rand(15))+vocals(Rand(5))+consonants(Rand(15))
        ElseIf r = 1
            name$ = name$ + vocals(Rand(5))
        ElseIf r = 2
            If c > -1
                name$ = name$ + consonants(c)
            Else 
                name$ = name$ + consonants(Rand(15))
            EndIf 
        EndIf        
    EndIf 
    
    Return name$
End Function 